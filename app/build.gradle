apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'io.fabric'

def versionMajor = 0
def versionMinor = 9
def versionPatch = 5

android {
	compileSdkVersion 29
	defaultConfig {
		applicationId "com.dimowner.audiorecorder"
		minSdkVersion 21
		targetSdkVersion 29
		versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
	}

	signingConfigs {
		dev {
			storeFile file('key/debug/debug.keystore')
			storePassword 'android'
			keyAlias 'androiddebugkey'
			keyPassword 'android'
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			minifyEnabled false
		}
	}

	flavorDimensions "default"

	productFlavors {
		debugConfig {
			dimension "default"
			applicationId "com.dimowner.audiorecorder.debug"
			minSdkVersion versions.minSdkVersion
			targetSdkVersion versions.targetSdkVersion
			signingConfig = signingConfigs.dev
		}
		releaseConfig {
			dimension "default"
			signingConfig = signingConfigs.dev
			applicationId "com.dimowner.audiorecorder"
			minSdkVersion versions.minSdkVersion
			targetSdkVersion versions.targetSdkVersion
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	lintOptions {
		abortOnError false
	}
}

repositories {
	maven { url 'https://maven.fabric.io/public' }
}

// Remove not needed buildVariants.
android.variantFilter { variant ->
	if (variant.buildType.name == 'release'
			&& variant.getFlavors().get(0).name == 'debugConfig') {
		variant.setIgnore(true)
	}
	if (variant.buildType.name == 'debug'
			&& variant.getFlavors().get(0).name == 'releaseConfig') {
		variant.setIgnore(true)
	}
}

dependencies {
	//Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"

	//Timber
	implementation "com.jakewharton.timber:timber:$versions.timber"
	implementation "androidx.recyclerview:recyclerview:$versions.androidX"

	implementation "androidx.viewpager2:viewpager2:1.0.0"
	implementation project(':recorderLib')
}
